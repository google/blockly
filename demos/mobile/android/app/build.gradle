apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId 'com.google.blockly.android.webview.demo'
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

task copyBlocklyHtmlFile(type: Copy) {
    from('../../html') {
        include 'index.html', 'toolbox_standard.js'
    }
    into project(':app').file('./src/main/assets/blockly')
    rename('index.html', 'webview.html')
}

task copyBlocklyDistFiles(type: Copy) {
    from('../../../../dist') {
        include 'blockly_compressed.js', 'blocks_compressed.js'
    }
    into project(':app').file('./src/main/assets/blockly')
}

task copyBlocklyBuildFiles(type: Copy) {
    from('../../../../build') {
        include 'msg/**'
    }
    into project(':app').file('./src/main/assets/blockly')
}

task copyBlocklyMediaFiles(type: Copy) {
    from('../../../..') {
        include 'media/**'
        exclude 'media/test_*'
    }
    into project(':app').file('./src/main/assets/blockly')
}

project.afterEvaluate {
    preBuild.dependsOn(copyBlocklyHtmlFile, copyBlocklyBuildFiles, copyBlocklyDistFiles, copyBlocklyMediaFiles)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
